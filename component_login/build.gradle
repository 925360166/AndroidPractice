if(loginRunAlone.toBoolean()){
    apply plugin: 'com.android.application'
}else{
    apply plugin: 'com.android.library'
}

android {
    def buildConfig = rootProject.extensions.getByName("ext")

    compileSdkVersion compile_sdk_version.toInteger()

    defaultConfig {
        if(loginRunAlone.toBoolean()){
            applicationId "com.example.songwei.component_login"
        }

        minSdkVersion buildConfig["minSdkVersion"]
        targetSdkVersion buildConfig["targetSdkVersion"]
        versionCode version_code.toInteger()
        versionName buildConfig["versionName"]
        testInstrumentationRunner buildConfig["testInstrumentationRunner"]
    }

    sourceSets{
        main{
            //独立运行与集成调试的时候，使用不同的manifest文件
            if(loginRunAlone.toBoolean()){
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }else{
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation project(':common_lib')
}
